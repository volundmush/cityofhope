@skip isdbref(tag(setr(1,equipsys)))={@assert/inline isdbref(globalroom())=@pemit %#=ERROR: No master room set!;@assert/inline isdbref(setr(0,create(Equipment Manager,,t)))=@pemit %#=ERROR: Could not create code object %q1: %q0;@set %q0=INDESTRUCTIBLE !NO_COMMAND;@tag/add %q1=%q0;@tel %q0=globalroom()}

&META_AUTHOR #equipsys=Sponge/#4
&META_PURPOSE #equipsys=Manage character equipment.
&META_UPDATED_BY #equipsys=Sponge/#4
@Startup #equipsys=@drain me;@notify me

@skip isdbref(tag(setr(1,equipdb)))={@assert/inline isdbref(tag(stor))=@pemit %#=ERROR: No storage object installed!;@assert/inline isdbref(setr(0,create(Periodic Cleanup Triggers,,t)))=@pemit %#=ERROR: Could not create code object: %q0;@set %q0=INDESTRUCTIBLE NO_COMMAND;@tag/add %q1=%q0;@tel %q1=#stor}

&DB_ID #equipsys=#3904

&LIST_TEMPLATE_BASE #equipsys=Name|Description|Legal
&LIST_TEMPLATE_WEAPON #equipsys=Damage|Conceal
&LIST_TEMPLATE_FIREARM #equipsys=Range|Clip|Registered|Rate
&LIST_TEMPLATES_FIREARM #equipsys=Firearm|Weapon
&UFUN_FMT_PROPERTY #equipsys=[ljust(pcol(before(%0,:):),10,.)][rjust(argcol(after(%0,:)),12,.)]
&UFUN_FMT_PROPERTIES #equipsys=column(iter(%0,u(UFUN_FMT_PROPERTY,##),|,|),24,|,2)
&UFUN_FMT_CATEGORIES #equipsys=[pcol(Categories:)] [itemize(iter(%0,argcol(##),|,|),|)]
&UFUN_FMT_ITEM #equipsys=[header(extract(%0,2,1,||) \([argcol(%1)][pcol(\))])]%r%b%b[u(UFUN_FMT_CATEGORIES,before(%0,||))]%r[u(UFUN_FMT_PROPERTIES,last(%0,||))][footer()]
&TRIG_ADD_ITEM #equipsys=&DATA_ITEM_[setr(0,get(setr(D,#equipdb)/DATA_NEXT_ID))] %qD=%0;&DATA_NEXT_ID %qD=[inc(%q0)];@notify me
&TRIG_TEST #equipsys=@wait me=@trigger me/TRIG_ADD_ITEM=Test
&UFUN_LIST_FMT_ITEM #equipsys=[rjust(argcol(%1),3)] [ljust(pcol(extract(%0,2,1,||)),35)] [ljust(u(UFUN_FMT_CATEGORIES,before(%0,||)),40)]
&UFUN_MATCH_ITEMS #equipsys=if(strmatch(%0,ALL),lattr(#equipdb/DATA_ITEM_*),iter(lattr(#equipdb/DATA_ITEM_*),if(match(before(get(#equipdb/##),||),%0,|),##)))
&TRIG_NOTE_ITEM #equipsys=&_DATA_NOTE_PRIVATE_[u(#14/UFUN_ENCODE_STAT,%1)] %0=[extract(setr(2,get(#equipdb/DATA_ITEM_[%2])),2,1,||)]%r[stripansi(u(UFUN_FMT_PROPERTIES,last(%q2,||)))]
&UFUN_VALID_PROPERTIES #equipsys=setdiff(setunion(iter(iter(%0,v(LIST_TEMPLATES_[##]),|,|),v(LIST_TEMPLATE_[##]),|,|),v(LIST_TEMPLATE_BASE),|),|,|,|)
&LIST_TEMPLATES_THROWN #equipsys=Weapon
&LIST_TEMPLATES_MELEE #equipsys=Weapon
&CMND_EQUIP_SETPROP #equipsys=$+equip/setprop */*=*:@switch [hasauthority(%#,SETSTATS)][t(setr(0,get(#equipdb/DATA_ITEM_[%0])))][t(member(u(UFUN_VALID_PROPERTIES,first(%q0,||)),setr(1,u(UFUN_NORM_PROPNAME,%1)),|))]=0*,@pemit %#=[errstr()] [pcol(To modify items you must have the authority)] [argcol(SETSTATS)],10*,@pemit %#=[errstr()] [argcol(%0)] [pcol(isn't a valid item.)],110*,@pemit %#=[errstr()] [argcol(%q1)] [pcol(is not a valid property for)] [argcol(extract(%q0,2,1,||))],111,{&DATA_ITEM_[%0] #equipdb=[extract(%q0,1,2,||)]||[u(UFUN_UPDATE_PROPERTY,last(%q0,||),%q1,%2)];@pemit %#=Updated.}
&UFUN_NORM_PROPNAME #equipsys=u(#14/UFUN_DECODE_STAT,u(#14/UFUN_ENCODE_STAT,%0))
&UFUN_UPDATE_PROPERTY #equipsys=setunion(setdiff(%0,grab(%0,%1:*,|),|),%1:%2,|)
&CMND_EQUIP_NEW #equipsys=$+equip/new *:@switch [hasauthority(%#,SETSTATS)][hasattrp(#equipdb,DATA_ITEM_%0)]=0*,@pemit %#=[errstr()] [pcol(To add new items you must have the authority)] [argcol(SETSTATS)],10*,@pemit %#=[errstr()] [pcol(There is no item number)] [argcol(%0)],11,{&DATA_ID_[setr(0,u(UFUN_RANDID))] #equipdb=Type:%0|Created:[secs()]/%#|Updated:[secs()]/%#;@pemit %#=[pcol(Created item)] [argcol(%q0)]}
&UFUN_RANDID #equipsys=rjust(rand(1000000),6,0)
&CMND_EQUIP_UNOWNED #equipsys=$+equip/unowned:@switch [hasauthority(%#,VIEWSTATS)]=0*,@pemit %#=[errstr()] [pcol(To list unowned equipment you must have the authority)] [argcol(VIEWSTATS)],1,@pemit %#=[header(Unowned Equipment)]%r[iter(u(UFUN_LIST_UNOWNED),u(UFUN_FMT_UNOWNED,##),%b,%r)]%r[footer()]
&UFUN_FMTF_TYPE #equipsys=pcol(Type:)[argcol(%0)]/[pcol(extract(get(#equipdb/DATA_ITEM_%0),2,1,||))]
&UFUN_FMTF_CREATED #equipsys=[pcol(Created:)][argcol(timefmt($Y$m$d-$H$M,before(%0,/)))] by [moniker(after(%0,/))]
&UFUN_FMTF #equipsys=localize(if(hasattrp(me,UFUN_FMTF_[setr(0,first(%0,:))]),u(UFUN_FMTF_[%q0],rest(%0,:)),[pcol(%q0:)][argcol(after(%0,:))]))
&UFUN_FMT_UNOWNED #equipsys=argcol(%0) - [localize(setq(0,get(#equipdb/DATA_ID_%0))[iter(Type Created,u(UFUN_FMTF,grab(%q0,##:*,|)))])]
&UFUN_LIST_UNOWNED #equipsys=sort(iter(lattr(#equipdb/DATA_ID_*),if(not(grab(get(#equipdb/##),Owner:*,|)),last(##,_))))
&CMND_EQUIP #equipsys=$+equip *:@switch [t(setr(1,u(UFUN_WHAT_ITEM,%0,%#)))]=0*,@pemit %#=[errstr()] [pcol(I can't identify the item)] [argcol(%0)],1,@pemit %#=u(UFUN_SHOW_ITEM,%q1)
&UFUN_FMTF_UPDATED #equipsys=[pcol(Updated:)][argcol(timefmt($Y$m$d-$H$M,before(%0,/)))] by [moniker(after(%0,/))]
&UFUN_FMTF_OWNER #equipsys=[pcol(Owner:)][argcol(moniker(%0))]
&CMND_EQUIP_CHOWN #equipsys=$+equip/chown *=*:@switch [hasauthority(%#,SETSTATS)][hasattrp(#equipdb,DATA_ID_%0)][t(setr(1,whois(%1)))]=0*,@pemit %#=[errstr()] [pcol(To chown equipment you must have the authority)] [argcol(SETSTATS)].,10*,@pemit %#=[errstr()] [pcol(No such item:)] [argcol(%0)],110*,@pemit %#=[errstr()] [pcol(I can't identify the player)] [argcol(%1)],111,{@trigger me/TRIG_EQUIP_CHOWN=%0,%q1,%#}
&TRIG_EQUIP_CHOWN #equipsys=@trigger me/TRIG_EQUIP_UPDATE=%0,Owner:%1|Offered To,%2;@pemit %2=[pcol(Item)] [argcol(%0)] ([setr(N,u(UFUN_FMTF,grab(setr(0,get(#equipdb/DATA_ID_%0)),Type:*,|)))]) [pcol(has been transferred to)] [argcol(moniker(%1))];@pemit %1=[argcol(moniker(%2))] [pcol(has transferred item)] [argcol(%0)] (%qN) [pcol(to you.)]
&UFUN_MAPUNSET #equipsys=setdiff(%0,grab(%0,%1:*,|),|)
&UFUN_MAPSET #equipsys=setunion(u(UFUN_MAPUNSET,%0,first(%1,:)),if(strlen(rest(%1,:)),%1),|)
&UFUN_MAPUPDATE #equipsys=fold(UFUN_MAPSET,%1,%0,|)
&TRIG_EQUIP_UPDATE #equipsys=&DATA_ID_%0 #equipdb=[u(UFUN_MAPUPDATE,get(#equipdb/DATA_ID_%0),%1|Updated:[secs()]/%2)]
&UFUN_MAPGET #equipsys=rest(grab(%0,%1:*,|),:)
&UFUN_LIST_WHO #equipsys=iter(0 1 2 3 4 5 6 7 8 9,u(UFUN_LIST_WHO_PART,%q0,##))
&CMND_EQUIP_WHO #equipsys=$+equip/who *:@switch [t(setr(0,whois(%0)))][corbool(strmatch(%q0,%#),hasauthority(%#,VIEWSTATS))]=0*,@pemit %#=[errstr()] [pcol(I can't identify the player)] [argcol(%0)],10*,@pemit %#=[errstr()] [pcol(To view someone else's equipment you must have the authority)] [argcol(VIEWSTATS)],11,{@pemit %#=[header(Equipment Owned By [argcol(moniker(%q0))])]%r[iter(u(UFUN_LIST_WHO,%q0),u(UFUN_FMT_LITEM,##),%b,%r)]%r[footer()]}
&CMND_EQUIP_NOARGS #equipsys=$+equip:@force %#=+equip/who %#
&UFUN_WHAT_ITEM #equipsys=localize(trim(if(candbool(isint(%0),eq(strlen(%0),6)),if(candbool(hasauthority(%1,VIEWSTATS),hasattrp(#equipdb,DATA_ID_%0)),%0,[setq(0,whois(%#))][setinter(%0,u(UFUN_LIST_WHO,%1))]),u(UFUN_WHAT_ITEM_BYNAME,%0,%1))))
&UFUN_WHAT_ITEM_BYNAME #equipsys=localize(if(eq(words(setr(0,iter(u(UFUN_LIST_WHO,%1),if(strmatch(u(UFUN_MAPGET,get(#equipdb/DATA_ID_##),Name),*%0*),##)))),1),%q0))
&CMND_EQUIP_NAME #equipsys=$+equip/name *=*:@switch [t(setr(0,u(UFUN_WHAT_ITEM,%0,%#)))]=0*,@pemit %#=[errstr()] [pcol(I can't identify the item)] [argcol(%0)],1,{@pemit %#=[pcol(Naming item)] [argcol(%q0)] ([u(UFUN_FMTF_TYPE,u(UFUN_MAPGET,get(#equipdb/DATA_ID_%q0),TYPE))]) to [setr(1,strip(%1,|:))];@trigger me/TRIG_EQUIP_UPDATE=%q0,Name:%q1,%#}
&CMND_EQUIP_SET #equipsys=$+equip/set *=*:@switch [hasauthority(%#,SETSTATS)][t(setr(0,u(UFUN_WHAT_ITEM,%0,%#)))][t(%1)]=0*,@pemit %#=[errstr()] [pcol(To modify equipment you must have the authority)] [argcol(SETSTATS)],10*,@pemit %#=[errstr()] [pcol(I can't identify the item)] [argcol(%0)],110*,@pemit %#=[errstr()] [pcol(What should I set?)],111,{@trigger me/TRIG_EQUIP_UPDATE=%q0,%1,%#;@force %#=+equip %q0}
&UFUN_FMT_LITEM #equipsys=argcol(%0) - [localize(setq(0,get(#equipdb/DATA_ID_%0))[iter(Type Name,u(UFUN_FMTF,grab(%q0,##:*,|)))])]
&CMND_EQUIP_SHOW #equipsys=$+equip/show *=*:@switch [t(setr(1,u(UFUN_WHAT_ITEM,%0,%#)))][t(setr(2,whois(%1)))]=0*,@pemit %#=[errstr()] [pcol(I can't identify the item)] [argcol(%0)],10*,@pemit %#=[errstr()] [pcol(I can't identify the player)] [argcol(%1)],11,{@pemit %#=[pcol(Showed)] [argcol(%q1)] ([u(UFUN_FMTF_TYPE,u(UFUN_MAPGET,get(#equipdb/DATA_ID_%q1),Type))]) [pcol(to)] [argcol(moniker(%q2))];@pemit %q2=u(UFUN_SHOW_ITEM,%q1)}
&UFUN_SHOW_ITEM #equipsys=[header(Item [argcol([if(setr(N,u(UFUN_MAPGET,setr(1,get(#equipdb/DATA_ID_%0)),Name)),%qN:%b)]%0)])]%r[u(UFUN_SHOW_ITEMPROP,%0)][footer()]
&CMND_EQUIP_TYPES_ITEM #equipsys=$+equip/types *:@switch [isnum(%0)][hasattr(#equipdb,DATA_ITEM_%0)]=0*,{@force %#=+equip/typeslist %0},10*,@pemit %#=[errstr()] [pcol(No such item:)] [argcol(%0)],11,@pemit %#=[u(UFUN_FMT_ITEM,get(#equipdb/DATA_ITEM_%0),%0)]
&CMND_EQUIP_TYPES_LIST #equipsys=$+equip/typeslist *:@switch [t(setr(0,u(UFUN_MATCH_ITEMS,%0)))]=0*,@pemit %#=[errstr()] [pcol(No such item category:)] [argcol(%0)],1,{@pemit %#=[header(Equipment: [argcol(%0)])]%r%b[iter(%q0,u(UFUN_LIST_FMT_ITEM,get(#equipdb/##),last(##,_)),%b,%r%b)]%r[footer()]}
&CMND_EQUIP_TYPES #equipsys=$+equip/types:@force %#=+equip/typeslist ALL
&UFUN_FMT_ITEMPROP #equipsys=sort([setq(T,last(get(#equipdb/DATA_ITEM_[u(UFUN_MAPGET,setr(0,get(#equipdb/DATA_ID_%0)),Type)]),||))][iter(%q0,[u(UFUN_FMTF,##)][if(strlen(u(UFUN_MAPGET,%qT,before(##,:))),*)],|,|)]|[trim(squish(iter(%qT,if(not(strlen(u(UFUN_MAPGET,%q0,before(##,:)))),u(UFUN_FMTF,##)),|,|),|),b,|)],,|)
&UFUN_SHOW_ITEMPROP #equipsys=localize(column(squish(trim(iter(u(UFUN_FMT_ITEMPROP,%0),if(lt(strlen(##),25),[before(##,:)]:[rjust(after(##,:),sub(24,strlen(before(##,:))),.)],setq(R,setunion(##,%qR,|))),|,|),,|),|),26,|,0)[if(%qR,column(squish(trim(iter(%qR[setq(R,)],if(lt(strlen(##),38),[before(##,:)]:[rjust(after(##,:),sub(37,strlen(before(##,:))),.)],setq(R,setunion(##,%qR,|))),|,|),,|),|),39,|,0)[if(%qR,iter(%qR,##,|,%r)%r)])])
&LIST_TEMPLATE_MAGIC #equipsys=Type|Level
&UFUN_LIST_WHO_PART #equipsys=sort(iter(lattr(#equipdb/DATA_ID_%1*),if(strmatch(%0,u(UFUN_MAPGET,get(#equipdb/##),Owner)),last(##,_))))


@@ Equipdb
&META_AUTHOR #equipdb=Sponge/#4
&META_PURPOSE #equipdb=House equipment data
&META_UPDATED_BY #equipdb=Sponge/#4
&DATA_NEXT_ID #equipdb=4
&DATA_ITEM_1 #equipdb=Firearm|Weapon||Light Pistol||Clip:17+1|Conceal:P|Damage:4|Range:20|Rate:4|Ref:WW3801/207
&DATA_ITEM_2 #equipdb=Firearm|Weapon||Light Revolver||Damage:4|Range:12|Rate:3|Clip:6|Conceal:P|Ref:WW3801/207
&DATA_ITEM_3 #equipdb=Firearm|Weapon||Heavy Revolver||Damage:6|Range:35|Rate:2|Clip:6|Conceal:J|Ref:WW3801/207
&DATA_ITEM_4 #equipdb=Firearm|Weapon||Heavy Pistol||Damage:5|Range:30|Rate:3|Clip:7+1|Conceal:J|Ref:WW3801/207
&DATA_ITEM_5 #equipdb=Firearm|Weapon||Rifle||Damage:8|Range:200|Rate:1|Clip:5+1|Conceal:N|Ref:WW3801/207
&DATA_ITEM_6 #equipdb=Firearm|Weapon||Small SMG||Damage:4|Range:25|Rate:3|Clip:30+1|Conceal:J|Burst:Y|Auto:Y|Spray:Y|Ref:WW3801/207
&DATA_ITEM_7 #equipdb=Firearm|Weapon||Large SMG||Damage:4|Range:50|Rate:3|Clip:30+1|Conceal:T|Burst:Y|Auto:Y|Spray:Y|Ref:WW3801/207
&DATA_ITEM_8 #equipdb=Firearm|Weapon||Assault Rifle||Damage:7|Range:150|Rate:3|Clip:42+1|Conceal:N|Burst:Y|Auto:Y|Spray:Y|Ref:WW3801/207
&DATA_ITEM_9 #equipdb=Firearm|Weapon||Pump Shotgun||Damage:8|Range:20|Rate:1|Clip:5+1|Conceal:T|Ref:WW3801/207
&DATA_ITEM_10 #equipdb=Firearm|Weapon||Semi-Auto Shotgun||Damage:8|Range:20|Rate:3|Clip:8+1|Conceal:T|Ref:WW3801/207
&DATA_ITEM_11 #equipdb=Firearm|Weapon||Short Bow||Damage:4|Range:60|Rate:1|Clip:1|Conceal:N|Ready Actions:1|Ref:WW3801/207
&DATA_ITEM_12 #equipdb=Firearm|Weapon||Long Bow||Clip:1|Conceal:N|Damage:5|Range:120|Rate:1|Ready Actions:1|Ref:WW3801/207
&DATA_ITEM_13 #equipdb=Firearm|Weapon||Crossbow||Damage:5|Range:90|Rate:1|Clip:1|Conceal:T|Ready Actions:2|Ref:WW3801/207
&DATA_ITEM_14 #equipdb=Melee|Weapon||Sap||Difficulty:4|Damage:Str|Dmg Type:B|Conceal:P|Ref:WW3801/210
&DATA_ITEM_15 #equipdb=Melee|Weapon||Chain||Difficulty:5|Damage:Str|Dmg Type:B|Conceal:J|Ref:WW3801/210
&DATA_ITEM_16 #equipdb=Melee|Weapon||Staff||Difficulty:6|Damage:Str+1|Dmg Type:B|Conceal:N|Ref:WW3801/210
&DATA_ITEM_17 #equipdb=Melee|Weapon||Mace||Difficulty:6|Damage:Str+2|Dmg Type:L|Conceal:N|Ref:WW3801/210
&DATA_ITEM_18 #equipdb=Melee|Weapon||Knife||Difficulty:4|Damage:Str|Dmg Type:L|Conceal:P|Ref:WW3801/210
&DATA_ITEM_19 #equipdb=Melee|Weapon||Club||Difficulty:5|Damage:Str+1|Dmg Type:B|Conceal:T|Ref:WW3801/210
&DATA_ITEM_20 #equipdb=Melee|Weapon||Sword||Difficulty:6|Damage:Str+2|Dmg Type:L|Conceal:T|Ref:WW3801/210
&DATA_ITEM_21 #equipdb=Melee|Weapon||Axe (Weapon)||Difficulty:7|Damage:Str+3|Dmg Type:L|Conceal:N|Ref:WW3801/210
&DATA_ITEM_22 #equipdb=Melee|Weapon||Axe (Lumber/Fire)||Difficulty:6|Damage:Str+2|Dmg Type:L|Conceal:T|Ref:WW3801/210
&DATA_ITEM_23 #equipdb=Melee|Weapon||Polearm||Difficulty:7|Damage:Str+3|Dmg Type:L|Conceal:N|Ref:WW3801/210
&DATA_ITEM_24 #equipdb=Melee|Weapon||Klaive||Difficulty:6|Damage:Str+2|Dmg Type:L/A|Conceal:J|Ref:WW3801/210
&DATA_ITEM_25 #equipdb=Melee|Weapon||Grand Klaive||Difficulty:7|Damage:Str+3|Dmg Type:L/A|Conceal:T|Ref:WW3801/210
&DATA_ITEM_26 #equipdb=Thrown|Weapon||Throwing Knife||Difficulty:6|Damage:Str|Dmg Type:L|Conceal:P|Ref:WW3801/208
&DATA_ITEM_27 #equipdb=Thrown|Weapon||Shuriken||Difficulty:7|Damage:3|Dmg Type:L|Conceal:P|Ref:WW3801/208
&DATA_ITEM_28 #equipdb=Thrown|Weapon||Stone (Small, Thrown)||Difficulty:5|Damage:Str-1|Dmg Type:B|Conceal:J|Ref:WW3801/208
&DATA_ITEM_29 #equipdb=Thrown|Weapon||Stone/Brick (Large, Thrown)||Difficulty:5|Damage:Str|Dmg Type:B|Conceal:J|Ref:WW3801/208
&DATA_ITEM_30 #equipdb=Thrown|Weapon||Spear||Difficulty:6|Damage:Str+1|Dmg Type:L|Conceal:N|Ref:WW3801/208
&DATA_ITEM_31 #equipdb=Thrown|Weapon||Swallow (Unladen)||Difficulty:9|Damage:Str-3|Dmg Type:B|Conceal:N
&DATA_ITEM_32 #equipdb=Firearm|Weapon||Sniper Rifle||Damage:8|Range:275|Rate:1|Clip:5|Conceal:N
&DATA_ITEM_33 #equipdb=Melee|Weapon||Machete||Damage:Str+1|Difficulty:5|Dmg Type:L|Conceal:T
&DATA_ITEM_34 #equipdb=Armor||Reinforced Clothing||Soak:1|Dex Penalty:0|Ref:WW3801/206
&DATA_ITEM_35 #equipdb=Armor||Armor T-shirt||Soak:2|Dex Penalty:1|Ref:WW3801/206
&DATA_ITEM_36 #equipdb=Armor||Kevlar Vest||Soak:3|Dex Penalty:1|Ref:WW3801/206
&DATA_ITEM_37 #equipdb=Armor||Flak Jacket||Soak:4|Dex Penalty:2|Ref:WW3801/206
&DATA_ITEM_38 #equipdb=Armor||Full Riot Gear||Soak:5|Dex Penalty:3|Ref:WW3801/206
&DATA_ITEM_0 #equipdb=Anything||Anything||
&DATA_ITEM_39 #equipdb=Magic||Fetish||Magic Type:Fetish|Level:1
&DATA_ITEM_40 #equipdb=Magic||Treasure||Magic Type:Treasure|Level:1
&DATA_ITEM_41 #equipdb=Magic||Wonder||Magic Type:Wonder|Level:1
&DATA_ITEM_42 #equipdb=Magic||Device||Magic Type:Device|Level:1
&DATA_ITEM_43 #equipdb=Firearm|Weapon||Taser||Damage:Stun until contact broken or trigger released|Range:5|Rate:1|Clip:1|Charge:200|Conceal:J
&DATA_ITEM_44 #equipdb=Firearm|Weapon||Police Taser||Damage:Stun until contact broken or trigger released|Range:10|Rate:1|Clip:3|Charge:500|Conceal:J